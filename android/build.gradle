buildscript {
  ext.getExtOrDefault = {name, fallback = null ->
    return rootProject.ext.has(name) ? rootProject.ext.get(name) : fallback
  }

  repositories {
    google()
    mavenCentral()
  }

  dependencies {
    classpath "com.android.tools.build:gradle:8.7.2"
    // noinspection DifferentKotlinGradleVersion
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${getExtOrDefault('kotlinVersion')}"
  }
}


apply plugin: "com.android.library"
apply plugin: "kotlin-android"

apply plugin: "com.facebook.react"

def getExtOrIntegerDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : (project.properties["ImagePicker_" + name]).toInteger()
}

android {
  namespace "com.imagepicker"

  compileSdkVersion getExtOrIntegerDefault("compileSdkVersion")

  defaultConfig {
    minSdkVersion getExtOrIntegerDefault("minSdkVersion")
    targetSdkVersion getExtOrIntegerDefault("targetSdkVersion")
  }

  buildFeatures {
    buildConfig true
  }

  buildTypes {
    release {
      minifyEnabled false
    }
  }

  lintOptions {
    disable "GradleCompatible"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  sourceSets {
    main {
      java.srcDirs += [
        "generated/java",
        "generated/jni"
      ]
    }
  }
}

repositories {
  mavenCentral()
  google()
}

def kotlin_version = getExtOrDefault("kotlinVersion")
def glide_version = getExtOrDefault("glideVersion", "5.0.0-rc01")
def picture_version = getExtOrDefault("pictureVersion", "v3.11.3")
def ucropVersion = getExtOrDefault("ucropVersion", "v3.11.2")
def compressVersion = getExtOrDefault("compressVersion", "v3.11.2")
def cameraxVersion = getExtOrDefault("cameraxVersion", "v3.11.2")

dependencies {
  implementation "com.facebook.react:react-android"
  implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
  implementation "com.github.bumptech.glide:glide:$glide_version"
  implementation "io.github.lucksiege:pictureselector:$picture_version"
  implementation "io.github.lucksiege:ucrop:$ucropVersion"
  implementation "io.github.lucksiege:compress:$compressVersion"
  implementation "io.github.lucksiege:camerax:$cameraxVersion"
}

react {
  jsRootDir = file("../src/")
  libraryName = "ImagePicker"
  codegenJavaPackageName = "com.imagepicker"
}
